plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'realm-android'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.bnikolov.java2daysdemo"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def signing_secret = System.getenv('JAVA2DAYS_SIGN_SECRET')
    signingConfigs {
        prodConfig {
            storeFile file("java2days_prod_keystore.jks")
            storePassword signing_secret
            keyAlias "java2daysProdAlias"
            keyPassword signing_secret
        }

        demoConfig {
            storeFile file("java2days_keystore.jks")
            storePassword signing_secret
            keyAlias "java2daysAlias"
            keyPassword signing_secret
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    flavorDimensions "version"
    productFlavors {
        production {
            dimension "version"
            buildConfigField "String", "SERVER_URL", '"https://api.github.com/"'
            manifestPlaceholders = [app_name: "Java2Days"]
            signingConfig signingConfigs.prodConfig
        }

        demo {
            dimension "version"
            buildConfigField "String", "SERVER_URL", '"https://demo-api.github.com/"'
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
            manifestPlaceholders = [app_name: "Java2Days-Demo"]
            signingConfig signingConfigs.demoConfig
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {

    // Main dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:$rootProject.kotlinCoreVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"

    // Hilt dependency injection
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"

    // Fragment navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navVersion"

    // Testing
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.junitExtVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoCoreVersion"
}